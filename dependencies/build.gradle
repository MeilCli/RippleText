apply plugin: "com.github.gmazzo.buildconfig"

buildConfig {
    packageName("net.meilcli.rippletext.gradle.dependencies")
    className("Dependencies")
}

ext {
    configUrl = ""
    configName = ""
}

@SuppressWarnings("GrMethodMayBeStatic")
void repositories(Closure closure) {
    closure()
}

@SuppressWarnings("GrMethodMayBeStatic")
void maven(Closure closure) {
    closure()
    buildConfig.forClass("Repositories") {
        buildConfigField("String", configName, "\"$configUrl\"")
    }
    ext.set("repository_$configName", configUrl)
}

@SuppressWarnings("GrMethodMayBeStatic")
void url(String url) {
    ext.configUrl = url
}

@SuppressWarnings("GrMethodMayBeStatic")
void name(String name) {
    ext.configName = name
}

@SuppressWarnings("GrMethodMayBeStatic")
void dependencies(Closure closure) {
    closure()
}

@SuppressWarnings("GrMethodMayBeStatic")
void library(String notice, String name, String className) {
    buildConfig.forClass(className) {
        buildConfigField("String", name, "\"$notice\"")
    }
    ext.set("library_${className}_${name}", notice)
}

repositories {
    maven {
        url "https://maven.google.com/"
        name "google"
    }
    maven {
        url "https://plugins.gradle.org/m2/"
        name "gradle"
    }
}

dependencies {
    ext.kotlinGroup = "org.jetbrains.kotlin"
    ext.kotlinVersion = "1.6.10"
    library "$kotlinGroup:kotlin-gradle-plugin:$kotlinVersion", "gradle", "Kotlin"
    library "$kotlinGroup:kotlin-stdlib-jdk7:$kotlinVersion", "stdlib", "Kotlin"
    library "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.6.1", "coroutinesCore", "Kotlin"
    library "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1", "coroutinesAndroid", "Kotlin"

    library "com.android.tools.build:gradle:7.2.1", "gradle", "Android"
    library "androidx.core:core-ktx:1.8.0", "core", "Android"
    library "androidx.appcompat:appcompat:1.4.2", "appCompat", "Android"
    library "androidx.emoji:emoji:1.1.0", "emoji", "Android"
    library "androidx.emoji:emoji-appcompat:1.1.0", "emojiAppCompat", "Android"
    library "androidx.emoji2:emoji2-views:1.1.0", "emoji2Views", "Android"
    library "androidx.recyclerview:recyclerview:1.2.1", "recyclerView", "Android"
    library "androidx.constraintlayout:constraintlayout:2.1.4", "constraintlayout", "Android"
    library "androidx.activity:activity-compose:1.4.0", "activityCompose", "Android"
    library "androidx.compose.ui:ui:1.2.0-rc02", "composeUi", "Android"
    library "androidx.compose.ui:ui-tooling:1.2.0-rc02", "composeUiTooling", "Android"
    library "androidx.compose.foundation:foundation:1.2.0-rc02", "composeFoundation", "Android"
    library "androidx.compose.material:material:1.2.0-rc02", "composeMaterial", "Android"
    library "androidx.test.ext:junit:1.1.3", "junit", "Android"
    library "androidx.test.espresso:espresso-core:3.4.0", "espresso", "Android"

    library "com.google.android.material:material:1.6.1", "material", "Google"
    library "com.google.android.material:compose-theme-adapter:1.1.11", "materialComposeThemeAdapter", "Google"

    library "org.jetbrains.dokka:dokka-gradle-plugin:1.6.10", "plugin", "Dokka"

    library "junit:junit:4.13.2", "junit", "Junit4"

    library "com.github.gmazzo:gradle-buildconfig-plugin:3.1.0", "gradleBuildConfigPlugin", "Gmazzo"
}